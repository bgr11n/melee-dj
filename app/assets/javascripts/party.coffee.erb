class @PartyVM
  constructor: (data) ->
    @nickname = data.nickname
    # TODO: Catch "Faye is not defined" - in case if ws server is down
    @client = new Faye.Client data.fayeServer

    @searcher = new SearcherVM @
    @playlist = new PlaylistVM @
    @chat = new ChatVM @

class SearcherVM
  constructor: (party) ->
    @party = party
    @query = ko.observable ''
    @results = ko.observableArray []

    @open = () -> $('#searcher-modal').foundation('reveal', 'open')

    @close = () -> $('#searcher-modal').foundation('reveal', 'close')

    @update = (data, event) =>
      if event.keyCode == 13 then @search() else true

    @search = () =>
      if @query().length > 0
        $.get '/api/v1/youtube/search', { q: @query() }, (res) =>
          for song in res
            @results.push new TrackVM(song)

    @addToPlaylist = (song) =>
      $.post "/api/v1/#{@party.nickname}/playlist/songs", { song: song }, (res) =>
        @query ''
        @results []
        do @close

class PlaylistVM
  constructor: (party) ->
    @party = party
    @songs = ko.observableArray []

    @subscription = @party.client.subscribe "/#{@party.nickname}/playlist/songs/new", (data) =>
      for song in JSON.parse(data)
        @songs.push new TrackVM(song.track)

    $(document).on 'ready page:change', =>
      # Sure we need this?
      @subscription.cancel()

    $.get "/api/v1/#{@party.nickname}/playlist/songs", (data) =>
      for song in data
        @songs.push new TrackVM(song.track)

class ChatVM
  constructor: (party) ->
    @party = party
    @messages = ko.observableArray []
    @newText = ko.observable ''

    @sendText = (data) =>
      if @newText().length > 0
        $.post data.action, { 'chat_message': { 'text': @newText() } }, (res) =>
          @newText ''

    @subscription = @party.client.subscribe "/#{@party.nickname}/chat/messages/new", (data) =>
      for message in JSON.parse(data)
        @messages.push new MessageVM(message)

    $(document).on 'ready page:change', =>
      # Sure we need this?
      @subscription.cancel()

class TrackVM
  constructor: (data) ->
    @uri = data.uri
    @title = data.title
    @thumbnail = data.thumbnail
    @duration = data.duration
    @created = data.created_at
    @user = new UserVM(data.user) if data.user

    @durationString = () =>
      hours = parseInt( @duration / 3600 ) % 24
      minutes = parseInt( @duration / 60 ) % 60
      seconds = @duration % 60

      minutes = if (minutes < 10) && (hours > 0) then "0" + minutes else minutes
      seconds = if seconds < 10 then "0" + seconds else seconds

      "#{if hours > 0 then hours + ':' else ''}" +
      "#{minutes + ':'}" +
      "#{seconds}"

class MessageVM
  constructor: (data) ->
    @text = data.text
    @created = data.created_at
    @user = new UserVM(data.user)

class UserVM
  constructor: (data) ->
    @email = data.email
    @nickname = data.nickname
    @grand = data.grand
